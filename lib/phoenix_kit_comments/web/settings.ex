defmodule PhoenixKitComments.Web.Settings do
  @moduledoc """
  LiveView for Comments module settings management.

  Manages:
  - `comments_enabled` toggle
  - `comments_moderation` toggle
  - `comments_max_depth` input
  - `comments_max_length` input

  ## Route

  Mounted at `{prefix}/admin/settings/comments`.
  """

  use PhoenixKitWeb, :live_view
  # Rebind gettext macros to the comments module's own catalogs (priv/gettext).
  use Gettext, backend: PhoenixKitComments.Gettext

  alias PhoenixKit.Settings
  alias PhoenixKit.Users.Auth.Scope

  @impl true
  # Gated on mount, not just on save. Every write here checked, while the
  # page itself rendered `@comments_giphy_api_key` into a form value — masked
  # by `type="password"`, which is not a security control — for any admin
  # without the comments permission.
  def mount(_params, _session, socket) do
    case check_authorization(socket) do
      :ok ->
        do_mount(socket)

      {:error, :unauthorized} ->
        {:ok,
         socket
         |> put_flash(:error, gettext("You do not have access to comments settings."))
         |> push_navigate(to: Routes.path("/admin"))}
    end
  end

  defp do_mount(socket) do
    socket =
      socket
      |> assign(:page_title, gettext("Comments Settings"))
      |> assign(:project_title, "")
      |> assign(:saving, false)
      |> assign(:editing_resource_type, nil)
      |> assign(:editing_path_value, "")
      |> assign(:editing_title_value, "")
      |> assign(:draft_paths, %{})
      |> assign(:draft_titles, %{})
      |> assign_settings_defaults()

    socket =
      if connected?(socket) do
        socket
        |> assign(:project_title, Settings.get_project_title())
        |> load_settings()
      else
        socket
      end

    {:ok, socket}
  end

  @impl true
  def handle_event("save", params, socket) do
    # Verify authorization before saving
    case check_authorization(socket) do
      {:error, :unauthorized} ->
        {:noreply, put_flash(socket, :error, gettext("Not authorized"))}

      :ok ->
        do_save_settings(params, socket)
    end
  end

  @impl true
  def handle_event("add_resource_path", %{"resource_path" => params}, socket) do
    case check_authorization(socket) do
      {:error, :unauthorized} ->
        {:noreply, put_flash(socket, :error, gettext("Not authorized"))}

      :ok ->
        do_add_resource_path(socket, params)
    end
  end

  @impl true
  def handle_event("remove_resource_path", %{"type" => resource_type}, socket) do
    case check_authorization(socket) do
      {:error, :unauthorized} ->
        {:noreply, put_flash(socket, :error, gettext("Not authorized"))}

      :ok ->
        templates = Map.delete(socket.assigns.resource_paths, resource_type)
        PhoenixKitComments.update_resource_path_templates(templates)

        {:noreply,
         socket
         |> put_flash(:info, gettext("Removed path for \"%{type}\"", type: resource_type))
         |> load_settings()}
    end
  end

  @impl true
  def handle_event("edit_resource_path", %{"type" => resource_type}, socket) do
    config = Map.get(socket.assigns.resource_paths, resource_type, %{})

    {:noreply,
     socket
     |> assign(:editing_resource_type, resource_type)
     |> assign(:editing_path_value, extract_path(config))
     |> assign(:editing_title_value, extract_title(config))}
  end

  @impl true
  def handle_event("cancel_edit_resource_path", _params, socket) do
    {:noreply,
     socket
     |> assign(:editing_resource_type, nil)
     |> assign(:editing_path_value, "")
     |> assign(:editing_title_value, "")}
  end

  @impl true
  def handle_event("live_edit_path", %{"resource_path" => params}, socket) do
    {:noreply,
     socket
     |> assign(:editing_path_value, params["path_template"] || socket.assigns.editing_path_value)
     |> assign(
       :editing_title_value,
       params["title_template"] || socket.assigns.editing_title_value
     )}
  end

  @impl true
  def handle_event("live_draft_path", %{"resource_path" => params}, socket) do
    resource_type = params["resource_type"] || ""
    path_value = params["path_template"] || ""
    title_value = params["title_template"] || ""
    draft_paths = Map.put(socket.assigns.draft_paths, resource_type, path_value)
    draft_titles = Map.put(socket.assigns.draft_titles, resource_type, title_value)

    {:noreply,
     socket
     |> assign(:draft_paths, draft_paths)
     |> assign(:draft_titles, draft_titles)}
  end

  @impl true
  def handle_event("save_resource_path", %{"resource_path" => params}, socket) do
    case check_authorization(socket) do
      {:error, :unauthorized} ->
        {:noreply, put_flash(socket, :error, gettext("Not authorized"))}

      :ok ->
        do_save_resource_path(socket, params)
    end
  end

  @impl true
  def handle_event("reset_defaults", _params, socket) do
    case check_authorization(socket) do
      {:error, :unauthorized} ->
        {:noreply, put_flash(socket, :error, gettext("Not authorized"))}

      :ok ->
        # Resource path templates are intentionally NOT reset here —
        # they are user-configured data, not settings with defaults.
        defaults = %{
          "comments_enabled" => "false",
          "comments_moderation" => "false",
          "comments_rich_text" => "true",
          "comments_max_depth" => "10",
          "comments_max_length" => "10000",
          "comments_giphy_enabled" => "false",
          "comments_giphy_api_key" => "",
          "comments_giphy_rating" => "g",
          "comments_attachments_enabled" => "false",
          "comments_max_attachments" => "4",
          "comments_attachment_max_size_mb" => "20"
        }

        Enum.each(defaults, fn {key, value} ->
          Settings.update_setting(key, value)
        end)

        {:noreply,
         socket
         |> put_flash(:info, gettext("Settings reset to defaults"))
         |> load_settings()}
    end
  end

  ## --- Private ---

  @allowed_settings ~w(
    comments_enabled
    comments_moderation
    comments_rich_text
    comments_max_depth
    comments_max_length
    comments_giphy_enabled
    comments_giphy_api_key
    comments_giphy_rating
    comments_attachments_enabled
    comments_max_attachments
    comments_attachment_max_size_mb
  )

  @numeric_settings %{
    "comments_max_depth" => {1, 50, "10"},
    "comments_max_length" => {100, 100_000, "10000"},
    "comments_max_attachments" => {1, 10, "4"},
    "comments_attachment_max_size_mb" => {1, 500, "20"}
  }

  defp do_save_settings(params, socket) do
    socket = assign(socket, :saving, true)
    settings = params |> Map.get("settings", %{}) |> sanitize_settings()

    try do
      results =
        Enum.map(settings, fn {key, value} ->
          Settings.update_setting(key, value)
        end)

      socket =
        if Enum.all?(results, fn
             {:ok, _} -> true
             _ -> false
           end) do
          socket
          |> put_flash(:info, gettext("Settings saved successfully"))
          |> load_settings()
        else
          put_flash(socket, :error, gettext("Failed to save some settings"))
        end

      {:noreply, assign(socket, :saving, false)}
    rescue
      e ->
        require Logger

        # The struct name only. This loop writes comments_giphy_api_key among
        # other settings, and Ecto/DBConnection exceptions in this class
        # (ChangeError, EncodeError, CastError) interpolate the offending
        # VALUE into their message — so logging Exception.message/1 here could
        # put the API key in plaintext in the log. Same stance the Giphy call
        # site already takes.
        Logger.error("Comment settings save failed: #{inspect(e.__struct__)}")

        {:noreply,
         assign(socket, :saving, false)
         |> put_flash(:error, gettext("Something went wrong. Please try again."))}
    end
  end

  defp sanitize_settings(settings) when is_map(settings) do
    settings
    |> Map.take(@allowed_settings)
    |> Enum.map(fn {key, value} -> {key, clamp_numeric(key, value)} end)
    |> Map.new()
  end

  defp sanitize_settings(_), do: %{}

  defp clamp_numeric(key, value) do
    case Map.get(@numeric_settings, key) do
      nil ->
        value

      {min, max, default} ->
        case Integer.parse(to_string(value)) do
          {n, _} -> n |> Kernel.max(min) |> Kernel.min(max) |> to_string()
          :error -> default
        end
    end
  end

  defp do_add_resource_path(socket, params) do
    resource_type = String.trim(params["resource_type"] || "")
    path_template = String.trim(params["path_template"] || "")
    title_template = String.trim(params["title_template"] || "")

    save_resource_config(socket, resource_type, path_template, title_template, fn socket ->
      socket
      |> assign(:draft_paths, Map.delete(socket.assigns.draft_paths, resource_type))
      |> assign(:draft_titles, Map.delete(socket.assigns.draft_titles, resource_type))
      |> put_flash(:info, gettext("Added path for \"%{type}\"", type: resource_type))
    end)
  end

  defp do_save_resource_path(socket, params) do
    resource_type = socket.assigns.editing_resource_type
    path_template = String.trim(params["path_template"] || "")
    title_template = String.trim(params["title_template"] || "")

    save_resource_config(socket, resource_type, path_template, title_template, fn socket ->
      socket
      |> assign(:editing_resource_type, nil)
      |> assign(:editing_path_value, "")
      |> assign(:editing_title_value, "")
      |> put_flash(:info, gettext("Updated path for \"%{type}\"", type: resource_type))
    end)
  end

  defp save_resource_config(socket, resource_type, path_template, title_template, on_success) do
    case validate_resource_path(resource_type, path_template) do
      :ok ->
        config = build_config(path_template, title_template)
        templates = Map.put(socket.assigns.resource_paths, resource_type, config)
        PhoenixKitComments.update_resource_path_templates(templates)

        {:noreply,
         socket
         |> on_success.()
         |> load_settings()}

      {:error, message} ->
        {:noreply, put_flash(socket, :error, message)}
    end
  end

  defp build_config(path_template, ""), do: %{"path" => path_template}

  defp build_config(path_template, title_template),
    do: %{"path" => path_template, "title" => title_template}

  defp validate_resource_path("", _), do: {:error, gettext("Resource type is required")}
  defp validate_resource_path(_, ""), do: {:error, gettext("Path template is required")}

  defp validate_resource_path(_resource_type, path_template) do
    cond do
      not (String.starts_with?(path_template, "/") or
               String.starts_with?(path_template, ":prefix")) ->
        {:error, gettext("Path template must start with / or :prefix")}

      String.contains?(path_template, "://") ->
        {:error, gettext("Path template must be a relative path")}

      not (String.contains?(path_template, ":uuid") or
               String.contains?(path_template, ":metadata.")) ->
        {:error, gettext("Path template must contain :uuid or :metadata.KEY placeholders")}

      true ->
        :ok
    end
  end

  defp assign_settings_defaults(socket) do
    socket
    |> assign(:comments_enabled, "false")
    |> assign(:comments_moderation, "false")
    |> assign(:comments_rich_text, "true")
    |> assign(:comments_max_depth, "10")
    |> assign(:comments_max_length, "10000")
    |> assign(:comments_giphy_enabled, "false")
    |> assign(:comments_giphy_api_key, "")
    |> assign(:comments_giphy_rating, "g")
    |> assign(:comments_attachments_enabled, "false")
    |> assign(:comments_max_attachments, "4")
    |> assign(:comments_attachment_max_size_mb, "20")
    |> assign(:resource_paths, %{})
    |> assign(:counts_by_type, %{})
    |> assign(:unconfigured_types, [])
    |> assign(:metadata_keys_by_type, %{})
  end

  defp load_settings(socket) do
    resource_paths = PhoenixKitComments.get_resource_path_templates()
    counts_by_type = PhoenixKitComments.count_comments_by_type()

    unconfigured_types =
      counts_by_type
      |> Map.keys()
      |> Enum.reject(&Map.has_key?(resource_paths, &1))
      |> Enum.sort()

    metadata_keys_by_type = PhoenixKitComments.list_metadata_keys_by_type()

    socket
    |> assign(:comments_enabled, Settings.get_setting("comments_enabled", "false"))
    |> assign(:comments_moderation, Settings.get_setting("comments_moderation", "false"))
    |> assign(:comments_rich_text, Settings.get_setting("comments_rich_text", "true"))
    |> assign(:comments_max_depth, Settings.get_setting("comments_max_depth", "10"))
    |> assign(:comments_max_length, Settings.get_setting("comments_max_length", "10000"))
    |> assign(:comments_giphy_enabled, Settings.get_setting("comments_giphy_enabled", "false"))
    |> assign(:comments_giphy_api_key, Settings.get_setting("comments_giphy_api_key", ""))
    |> assign(:comments_giphy_rating, Settings.get_setting("comments_giphy_rating", "g"))
    |> assign(
      :comments_attachments_enabled,
      Settings.get_setting("comments_attachments_enabled", "false")
    )
    |> assign(:comments_max_attachments, Settings.get_setting("comments_max_attachments", "4"))
    |> assign(
      :comments_attachment_max_size_mb,
      Settings.get_setting("comments_attachment_max_size_mb", "20")
    )
    |> assign(:resource_paths, resource_paths)
    |> assign(:counts_by_type, counts_by_type)
    |> assign(:unconfigured_types, unconfigured_types)
    |> assign(:metadata_keys_by_type, metadata_keys_by_type)
  end

  defp extract_path(config) when is_binary(config), do: config
  defp extract_path(%{"path" => path}), do: path
  defp extract_path(_), do: ""

  defp extract_title(config) when is_binary(config), do: ""
  defp extract_title(%{"title" => title}) when is_binary(title), do: title
  defp extract_title(_), do: ""

  defp check_authorization(socket) do
    scope = socket.assigns[:phoenix_kit_current_scope]

    if scope && Scope.has_module_access?(scope, "comments") do
      :ok
    else
      {:error, :unauthorized}
    end
  end

  @doc """
  Lightweight in-card section heading (icon + title + rule). Local copy of
  core's `Core.FormSection.section_header/1` under a distinct name, so this
  package renders identically without requiring a core release that exports
  it (and won't conflict with the core import once it does).
  """
  attr(:icon, :string, required: true)
  attr(:title, :string, required: true)
  attr(:class, :string, default: nil)

  def settings_section_header(assigns) do
    ~H"""
    <div class={["flex items-center gap-2 pt-4 first:pt-0", @class]}>
      <.icon name={@icon} class="w-4 h-4 text-primary/70" />
      <h3 class="text-sm font-semibold uppercase tracking-wide text-base-content/60">
        {@title}
      </h3>
      <div class="flex-1 border-t border-base-300 ml-2"></div>
    </div>
    """
  end
end
